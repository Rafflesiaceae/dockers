apply plugin: 'groovy'

sourceCompatibility = '1.8'

sourceSets {
    gdsl {
        groovy {
            srcDirs 'gdsl'
            compileClasspath = compileGroovy.classpath
        }
    }
    jenkinsfiles {
        groovy {
            srcDirs 'jenkinsfiles'
            compileClasspath = compileGroovy.classpath
        }
    }
    vars {
        groovy {
            srcDirs 'vars'
            compileClasspath = compileGroovy.classpath
        }
    }
}

configurations {
    jenkinsPlugins {}
}

configurations {
    all {
        exclude group: 'org.slf4j', module: 'slf4j-jdk14'
        resolutionStrategy {
            force 'com.ibm.icu:icu4j:65.1'
        }
    }
}

apply from: 'repositories.gradle'
apply from: 'jenkins_plugins.gradle'

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.4.17'

    testImplementation 'cglib:cglib-nodep:3.1'
    testImplementation 'com.homeaway.devtools.jenkins:jenkins-spock:2.0.1'
    testImplementation 'com.lesfurets:jenkins-pipeline-unit:1.1'
    testImplementation 'javax.servlet:javax.servlet-api:4.0.1'
    testImplementation 'org.objenesis:objenesis:2.1'
    testImplementation 'org.spockframework:spock-core:1.0-groovy-2.4'
    testImplementation 'org.jenkins-ci.main:jenkins-core:2.190.2'
    testImplementation configurations.jenkinsPlugins
}

task cleanScriptedPipelinesTestData(type: Delete) {
    delete 'target/test-classes'
}

task prepareSharedLibraries(type: Copy) {
    dependsOn tasks.cleanScriptedPipelinesTestData
    from 'vars'
    include '*'
    into 'target/test-classes'
}

task prepareJenkinsfiles(type: Copy) {
    dependsOn tasks.prepareSharedLibraries
    from 'jenkinsfiles'
    include '*'
    into 'target/test-classes'
}

test {
    dependsOn tasks.prepareJenkinsfiles
}
